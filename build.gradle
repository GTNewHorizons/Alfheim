apply plugin: 'forge'
apply plugin: 'kotlin'
apply plugin: 'idea'

buildscript {
	ext.kotlinVersion = '1.3.41'
	
	repositories {
		mavenCentral()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
		classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
	}
}

repositories {
	maven {
		name = "chickenbones"
		url = "http://chickenbones.net/maven/"
	}
	mavenCentral()
}

version = "BETA-pre19.18"
group= "main"
archivesBaseName = "Alfheim"

minecraft {
	version = "1.7.10-10.13.4.1614-1.7.10"
	runDir = "eclipse"
}

configurations {
	embed
	compile.extendsFrom(embed)
}

dependencies {
	compile "codechicken:CodeChickenLib:1.7.10-1.1.3.140:dev"
	compile "codechicken:CodeChickenCore:1.7.10-1.0.7.47:dev"
	compile "codechicken:NotEnoughItems:1.7.10-1.0.5.120:dev"
	
	compile fileTree(dir: 'lib', include: ['*.jar'])
	embed "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlinVersion"
}

compileJava {
	options.encoding = "UTF-8"
	sourceCompatibility = targetCompatibility = '1.8'
}

sourceCompatibility = targetCompatibility = '1.8'
compileKotlin.kotlinOptions.jvmTarget = "1.8"

jar {
	from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
	manifest {
		attributes 'FMLAT': 'alfheim_at.cfg'
		attributes 'FMLCorePlugin': 'alfheim.common.core.asm.AlfheimHookLoader'
		attributes 'FMLCorePluginContainsFMLMod': 'true'
	}
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}
		
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
	
	rename '(.+_at.cfg)', 'META-INF/$1'
}

idea {
	module {
		inheritOutputDirs = false
		outputDir = file('build/classes/main/')
	}
}

subprojects {
	apply plugin: 'idea'
}