apply plugin: 'forge'
apply plugin: 'kotlin'

buildscript {
	repositories {
		mavenCentral()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
		classpath 'org.jetbrains.kotlin:kotlin-gradle-plugin:1.2.50'
	}
}

version = "BETA-pre5.25"
group= "main"
archivesBaseName = "Alfheim"

minecraft {
	version = "1.7.10-10.13.4.1614-1.7.10"
	runDir = "eclipse"
}

configurations {
	embed
	compile.extendsFrom(embed)
}

dependencies {
	compile fileTree(dir: 'lib', include: ['*.jar'])
	embed 'org.jetbrains.kotlin:kotlin-stdlib:1.2.50'
	embed 'org.jetbrains.kotlin:kotlin-reflect:1.2.50'
	embed 'org.jetbrains.kotlin:kotlin-runtime:1.2.50'
}

compileJava {
	targetCompatibility = '1.6'
	sourceCompatibility = '1.6'
}

jar {
	from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
	manifest {
		attributes 'FMLAT': 'alfheim_at.cfg'
		attributes 'FMLCorePlugin': 'alfheim.common.core.asm.AlfheimHookLoader'
		attributes 'FMLCorePluginContainsFMLMod': 'true'
	}
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", project.minecraft.version

	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version':project.version, 'mcversion':project.minecraft.version
	}
		
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
	
	rename '(.+_at.cfg)', 'META-INF/$1'
}